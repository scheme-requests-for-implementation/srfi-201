<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>SRFI 201: Syntactic Extensions to the Core Scheme Bindings</title>
    <link href="favicon.png" rel="icon" sizes="192x192" type="image/png">
    <link rel="stylesheet" href="/srfi.css" type="text/css">
    <meta name="viewport" content="width=device-width, initial-scale=1"></head>
  <body>
    <h1><a href="https://srfi.schemers.org/"><img class="srfi-logo" src="https://srfi.schemers.org/srfi-logo.svg" alt="SRFI logo" /></a>201: Syntactic Extensions to the Core Scheme Bindings</h1>

<p>by Panicz Maciej Godek</p>

<h2>Status</h2>

<p>This SRFI is currently in <em>draft</em> status.  Here is <a href="https://srfi.schemers.org/srfi-process.html">an explanation</a> of each status that a SRFI can hold.  To provide input on this SRFI, please send email to <code><a href="mailto:srfi+minus+201+at+srfi+dotschemers+dot+org">srfi-201@<span class="antispam">nospam</span>srfi.schemers.org</a></code>.  To subscribe to the list, follow <a href="https://srfi.schemers.org/srfi-list-subscribe.html">these instructions</a>.  You can access previous messages via the mailing list <a href="https://srfi-email.schemers.org/srfi-201">archive</a>.</p>
<ul>
  <li>Received: 2020-06-19</li>
  <li>60-day deadline: 2020-08-24</li>
  <li>Draft #1 published: 2020-06-25</li>
  <li>Draft #2 published: 2020-11-12</li>
</ul>

<h2>Abstract</h2>

<p>This document describes a handful of syntactic extensions
  to the core bindings of the Scheme programming language.
  In particular, it proposes to extend the binding forms
  <code>lambda</code>, <code>let</code>,
  <code>let*</code> with pattern matching capabilities, to extend the forms <code>let</code>,  <code>let*</code>
  and <code>or</code> with the ability
  to handle multiple values, and to extend the form <code>define</code> with
  the ability of defining "curried" functions.</p>

<p>It also proposes a body-less variant of
  the <code>lambda</code> form, and specifies the
  behavior of body-less "curried" <code>define</code>
  forms to behave consistently with that extension.</p>

<h2>Issues</h2>

None at present.

<h2>Rationale</h2>

<p>The Scheme programming language is rich in syntactic extensions.
  For example, various pattern matching libraries mentioned in
  SRFI 200 define special forms like <code>match-lambda*</code>,
  <code>match-let</code> or <code>match-let*</code>, which extend
  the <code>lambda</code>, <code>let</code> and <code>let*</code>
  forms with the capability of pattern matching.</p>

<p>However, those forms usually do not take into account the
  ability of Scheme procedures to return multiple values, which
  is a trait that wasn't supported conveniently in the core
  language and had to be accounted for in SRFI 8, SRFI 11 and
  SRFI 71 (and remains forgotten by the <code>or</code> form).
</p>

<p>Various Scheme implementations provide the feature of
  "curried definitions", which allows one to generalize the
  <code>define</code>'s extended syntax
  <code>(define (function arguments ...) . body)</code>
  to produce higher-order functions. However, they do not
  integrate well with the pattern-matching variant of
  the <code>lambda</code> form.</p>

<p>The purpose of this document is to propose a set of
  extensions to the core bindings that integrate well with
  both pattern matching and multiple values. These extensions
  remain mostly backwards-compatible with earlier usages
  of the core forms.</p>

<h2>Specification</h2>

<h2>The pattern matching (destructuring) <code>lambda</code> form</h2>

<p>This document assumes that the <code>lambda</code> form
  should allow one to destructure its arguments, i.e.
  for example</p>

<pre>
  (map (lambda (`(,x . ,y))
         (+ x y))
       '((1 . 2) (3 . 4) (5 . 6)))
</pre>

<p>should be equivalent to</p>

<pre>
  (map (lambda (x)
         (+ (car x) (cdr x)))
       '((1 . 2) (3 . 4) (5 . 6)))
</pre>

<p>or, more generally,</p>

<pre>
  (lambda<sub>match</sub> (&lt;patterns&gt; ...) . &lt;body&gt;)
</pre>

<p>is equivalent to</p>

<pre>
  (lambda<sub>core</sub> args
    (match args
      (`(,&lt;patterns&gt; ...) . &lt;body&gt;)
      (_ (error "invalid args: "args", expected"'(&lt;patterns&gt; ...)))))
</pre>

<p>Moreover, in order to preserve backwards compatibility
  and avoid unnecessary performance penalties, if each
  <code>&lt;pattern&gt;</code> is an identifier,
  then <code>lambda<sub>match</sub></code> desugars
  directly into <code>lambda<sub>core</sub></code>.</p>

<h2>Empty bodies in the <code>lambda</code> form</h2>

<p>The ability to pattern-match arguments provides
  a justification for <code>lambda</code> forms
  with empty bodies: it makes sense to interpret</p>

<pre>
  (lambda<sub>match</sub> (&lt;patterns&gt; ...))
</pre>

as

<pre>
  (lambda<sub>core</sub> args
    (match args
      (`(,&lt;patterns&gt; ...) #t)
      (_ #f)))
</pre>

<h2>The pattern-matching <code>let</code> form</h2>

<p>The <code>let</code> form is often explained in terms of the <code>lambda</code>
  form, and therefore should be expected to inherit the ability to
  perform destructuring bind.</p>

<p>Additionally, Scheme provides a variant of the <code>let</code> form
  known as "named <code>let</code>", which is also expected to pattern-match.</p>

<p>Moreover, if there is only one expression whose value is being bound using
  the <code>let</code> form, it should also be able to handle multiple values
  (like SRFI 71, but with the ability to destructure the received values).</p>

<p>To give an example, the form could be used in the following way:</p>

<pre>
  (let lp ((`(,a ,b) `(,c) (values '(1 2) '(3))))
    (+ a b c))
</pre>

<p>Following SRFI 71, we treat the <code>values</code>
  keyword in the left position specially, so that the
  above form is equivalent to</p>

<pre>
  (let lp (((values `(,a ,b) `(,c)) (values '(1 2) '(3))))
    (+ a b c))
</pre>

<p>Note however, that if there is more than one expression whose value
  is being bound with the <code>let</code> form, and at least one of
  the bindings binds multiple values, then even though it is possible
  to implement this scenario, it is impossible to implement it efficiently,
  and because of this, such use should be discouraged.</p>

<h2>The pattern-matching <code>let*</code> form that supports multiple values</h2>

<p>The <code>let*</code> form in the Scheme language is typically explained
  in terms of nested <code>let</code> forms. Because of this, the bindings
  in the <code>let*</code> form are also expected to perform destructuring bind
  and to support multiple values (like SRFI 71, but with the ability
  to destructure additional values).</p>

<p>Here's an example use of the extended <code>let*</code> form:</p>

<pre>
  (let* ((`(,a ,b) `(,c) (values '(1 2) '(3)))
         ((d e `(,f . ,_) (values 4 5 '(6 7)))))
    (+ a b c d e f))
</pre>

<h2>The <code>define</code> form that supports "curried definitions"
  with destructuring</h2>

<p>The Scheme language provides a variant of the <code>define</code> form
  which is used like this:</p>

<pre>
  (define (function . args) . body)
</pre>

which desugars to

<pre>
  (define function (lambda args . body))
</pre>

By analogy, some Scheme implementations provide a generalization of this
ability, which allows one to define higher-order functions conveniently. This
feature, sometimes incorrectly called "currying", works by desugaring

<pre>
  (define ((head . tail) . args) . body)
</pre>

into

<pre>
  (define (head . tail) (lambda args . body))
</pre>

<p>This SRFI document requires that the <code>lambda</code> used in the desugared
  form supports destructuring.</p>

<p>Moreover, since <code>lambda</code> forms with
  empty bodies expand to predicates that say whether
  given arguments satisfy the specified patterns
  (rather than report an error), the behavior of
  body-less "curried" definitions is made consistent
  with the behavior of body-less <code>lambda</code>
  forms: all levels of arguments are tested against
  the specified patterns, and if some arguments do not
  comply with the requested patterns, the final procedure
  evaluates to <code>#f</code>; otherwise, i.e. if all
  arguments conform to their corresponding patterns,
  the final procedure evaluates to <code>#t</code>.
</p>

<h2>The <code>or</code> form that supports multiple values</h2>

<p>The <code>or</code> binding provided by default in the Scheme language exposes
  asymmetrical behavior with regard to handling multiple values in its different
  branches.</p>

<p>In particular, even though <code>(or #f (values 1 2))</code> yields
  the values <code>1</code> and <code>2</code>, the expression
  <code>(or (values 1 2) #f)</code> only yields <code>1</code> on
  some implementations, and on other implementations is not supported.</p>

<p>Since the handling of multiple values in the <code>or</code> form may
  require heap allocation, multiple-value <code>or</code> is considered
  an optional part of this SRFI.</p>


<h2>Implementation</h2>

<p>The sample implementations are based on the abilities of some Scheme
module systems to shadow existing bindings. They also rely on the presence
  of a SRFI-200-conformant pattern matching library, and the presence of
the SRFI 1 <code>every</code> procedure available during macro expansion.</p>

<h2>Pattern-matching <code>lambda</code></h2>

<p>The pattern-matching variant of the  <code>lambda</code> form
  can be implemented by defining the following <code>mlambda</code>
  form and exporting it to shadow the core <code>lambda</code> form.</p>

<pre>
(define-syntax mlambda
  (lambda (stx)
    (syntax-case stx ()

      ((_ args)
       #'(lambda args*
	   (match args*
	     (args #t)
	     (_ #f))))

      ((_ (first-arg ... last-arg . rest-args) . body)
       (and (every identifier? #'(first-arg ... last-arg))
	    (or (identifier? #'rest-args) (null? #'rest-args)))
       #'(lambda (first-arg ... last-arg . rest-args) . body))

      ((_ arg body ...)
       (or (identifier? #'arg) (null? #'arg))
       #'(lambda arg body ...))

      ((_ args body ...)
       #'(lambda params
           (match params
             (args body ...)
	     (_ (error 'mlambda '(args body ...)))))))))
</pre>

<h2>Higher-order <code>define</code> form</h2>

<p>The "currying" variant of the  <code>define</code> form
  can be implemented by defining the following <code>cdefine</code>
  form and exporting it to shadow the core <code>define</code> form
  (assuming the presence of the <code>mlambda</code> form defined
  in the previous section)</p>

<pre>
(define-syntax cdefine
  (syntax-rules ()
    ((_ (prototype . args))
     ;; the body-less variant: see below
     (cdefine-bodyless (prototype . args) #t))
    ((_ ((head . tail) . args) body ...)
     (cdefine (head . tail)
	      (mlambda args body ...)))
    ((_ (function . args) body ...)
     (define function
       (mlambda args body ...)))
    ((_ . rest)
     (define . rest))))

(define-syntax cdefine-bodyless
  (syntax-rules ()
   ((_ (function . pattern) result . args)
    (cdefine-bodyless function
		      (match arg
			(pattern result)
			(_ #f))
		      arg . args))
   
   ((_ name result args ... arg)
    (cdefine-bodyless name (lambda arg result) args ...))

   ((_ name value)
    (define name value))))
</pre>

<h2>Pattern-matching variant of the <code>let</code> form that supports multiple values</h2>

<p>The pattern-matching variant of the <code>let</code> form
  can be implemented by defining the following <code>named-match-let-values</code>
  form and exporting it to shadow the core <code>let</code> form.</p>

<pre>
(define-syntax match-let/error
  (syntax-rules ()
    ((_ ((structure expression) ...)
	body + ...)
     ((lambda args
	(match args
	  ((structure ...) body + ...)
	  (_ (error 'match-let/error
		    (current-source-location) 
		    '((structure expression) ...)
		    expression ...))))
      expression ...))))

(define-syntax named-match-let-values
  (lambda (stx)
    (syntax-case stx (values)
      ((_ ((identifier expression) ...) 
	  body + ...)
       (every identifier? #'(identifier ...))
       ;; optimization: plain "let" form
       #'(let ((identifier expression) ...)
	   body + ...))

      ((_ name ((identifier expression) ...) 
	  body + ...)
       (and (identifier? #'name)
	    (every identifier? #'(identifier ...)))
       ;; optimization: regular named-let
       #'(let name ((identifier expression) ...)
	   body + ...))

      ((_ name (((values . structure) expression))
	  body + ...)
       (identifier? #'name)
       #'(letrec ((name (mlambda structure body + ...)))
	   (call-with-values (lambda () expression) name)))

      ((_ (((values . structure) expression)) body + ...)
       #'(call-with-values (lambda () expression)
	   (mlambda structure body + ...)))

      ((_ name ((structure expression) ...)
	  body + ...)
       (and (identifier? #'name)
	    (any (lambda (pattern)
		   (display pattern)(newline)
		   (and (pair? pattern)
			(eq? (car pattern) 'values)))
		 (syntax->datum #'(structure ...))))
       #'(syntax-error "let can only handle one binding \
in the presence of a multiple-value binding"))
      
      ((_ name ((structure expression) ...)
	  body + ...)
       (identifier? #'name)
       #'(letrec ((name (mlambda (structure ...) body + ...)))
	   (name expression ...)))

      ((_ ((structure expression) ...)
	  body + ...)
       (any (lambda (pattern)
	      (and (pair? pattern)
		   (eq? (car pattern) 'values)))
	    (syntax->datum #'(structure ...)))
       #'(syntax-error "let can only handle one binding \
in the presence of a multiple-value binding"))
      
      ((_ ((structure expression) ...)
	  body + ...)
       #'(match-let/error ((structure expression) ...) 
			  body + ...))

      ((_ ((structure structures ... expression)) body + ...)
       #'(call-with-values (lambda () expression)
	   (mlambda (structure structures ... . _)
		    body + ...)))

      ((_ name ((structure structures ... expression))
		body + ...)
       (identifier? #'name)
       #'(letrec ((name (mlambda (structure structures ...)
			     body + ...)))
	   (call-with-values (lambda () expression) name)))
    )))
</pre>


<h2>Pattern-matching variant of the <code>let*</code> form that supports multiple values</h2>

<p>The pattern-matching variant of the <code>let*</code> form
  can be implemented by defining the following <code>match-let*-values</code>
  form and exporting it to shadow the core <code>let*</code> form. It assumes the
  presence of the definition of the <code>named-match-let-values</code> form from
  the previous section</p>

<pre>
(define-syntax match-let*-values
  (lambda (stx)
    (syntax-case stx ()
      ((_ ((identifier expression) ...) body + ...)
       (every identifier? #'(identifier ...))
       ;; optimization/base case: regular let*
       #'(let* ((identifier expression) ...)
	   body + ...))
      ((_ (binding . bindings) body + ...)
       #'(named-match-let-values (binding)
				 (match-let*-values
				  bindings
				  body + ...))))))

</pre>

<h2>A variant of the <code>or</code> form that supports multiple values</h2>

<p>The variant of the <code>or</code> form that supports multiple values
  can be implemented by defining the following <code>or/values</code>
  form and exporting it to shadow the core <code>or</code> form.</p>

<pre>
(define-syntax or/values
  (syntax-rules ()
    ((_)
     #false)

    ((or/values final)
     final)

    ((or/values first . rest)
     (call-with-values (lambda () first)
       (lambda result
	 (if (and (pair? result) (car result))
	     (apply values result)
	     (or/values . rest)))))))
</pre>

<h2>Specific implementations</h2>

<p>The implementation of this SRFI relies on the ability of module
  system to rename/shadow existing bindings.</p>

<p>The repository of this SRFI contains one implementation
  for <a href="https://github.com/scheme-requests-for-implementation/srfi-201/blob/master/guile/srfi/srfi-201.scm">Guile</a>,
  and another for <a href="https://github.com/scheme-requests-for-implementation/srfi-201/blob/master/racket/srfi/201.rkt">Racket</a>.
  Each comes with a test suite written in a literate programming style,
  and either of those test suites - i.e. the one for
  <a href="https://github.com/scheme-requests-for-implementation/srfi-201/blob/master/guile/examples.scm">Guile</a> and
  the one for <a href="https://github.com/scheme-requests-for-implementation/srfi-201/blob/master/racket/examples.rkt">Racket</a>
  - is an important supplement to this document.</p>

<p>We encourage the maintainers of Scheme implementations
  that do not support renaming/shadowing of core bindings, to
  extend their core bindings with the capabilities described
  in this document.</p>
  
<h2>Acknowledgements</h2>

<p>First of all, I'd like to thank Ścisław Dercz for being
  an early adopter of the <code><a href="https://github.com/plande/grand-scheme">(grand scheme) glossary</a></code>,
  which has been the base for the set of extensions proposed
  in this document.
  I am particularly proud of the fact that Dercz chose
  (grand scheme) to write a set of his programming doodles
  that he called <a href="https://github.com/drcz/useless-software">Useless Software</a>.
</p>

<p>I'm also very grateful to the developers of Guile,
  who, through their <code>(ice-9 curried-definitions)</code>
  module, taught me that overriding core Scheme forms is not
  only conceivable, but can also be very useful.</p>

<p>I consider this work to be a part of the decades-long
  evolution in functional programming style, dating back
  at least to Robin Milner and Rod Burstall, who pioneered
  pattern matching in programming languages.</p>

<p>This work was originally based on the pattern matcher
  written by Alex Shinn, based on the specification
  presented by Andrew K. Wright and Robert Cartwright.
  The Guile implementation of this SRFI still uses
  Shinn's <code>match</code> as its base.</p>

<p>The Racket version of this SRFI owes its existence
  to the kindness of the Racket community, and in particular
  to Jesse Alama, who invited me to speak at Racketfest
  that he organized in Berlin in 2019.</p>

<p>Last but not least, I'd like to thank all the people
  who persist in carrying the torch of Scheme development
  under the umbrella of the SRFI process, in particular
  to Arthur Gleckler for his patience, encouragement
  and support, Marc Nieper Wißkirchen for his attentive
  and insightful remarks, and John Cowan for shepherding
  the Scheme community.</p>

<h2>Copyright</h2>
Copyright &copy; Panicz Maciej Godek (2020).

<p>
  Permission is hereby granted, free of charge, to any person
  obtaining a copy of this software and associated documentation files
  (the "Software"), to deal in the Software without restriction,
  including without limitation the rights to use, copy, modify, merge,
  publish, distribute, sublicense, and/or sell copies of the Software,
  and to permit persons to whom the Software is furnished to do so,
  subject to the following conditions:

<p>
  The above copyright notice and this permission notice (including the
  next paragraph) shall be included in all copies or substantial
  portions of the Software.
<p>
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
  MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
  NONINFRINGEMENT.  IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
  BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
  ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
  CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

  <hr>
  <address>Editor: <a href="mailto:srfi-editors+at+srfi+dot+schemers+dot+org">Arthur A. Gleckler</a></address></body></html>
